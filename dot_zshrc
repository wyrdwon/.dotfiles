# =========================================================
# Powerlevel10k Instant Prompt (must be first)
# =========================================================
P10K_INSTANT="${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
[[ -r $P10K_INSTANT ]] && source $P10K_INSTANT

# =========================================================
# Environment
# =========================================================
export EDITOR=nvim
export BUN_INSTALL="$HOME/.bun"

# =========================================================
# PATH (prepend once, avoid repetition)
# =========================================================
path=(
  $HOME/.local/bin
  $HOME/packages/lua-language-server/bin
  $HOME/packages/nvim-linux-x86_64/bin
  $HOME/.npm-global/bin
  $HOME/.cargo/bin
  $BUN_INSTALL/bin
  $path
)

# =========================================================
# History & Shell Behavior
# =========================================================
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=$HISTSIZE
HISTDUP=erase

setopt \
  appendhistory sharehistory autocd \
  extendedglob nomatch beep \
  hist_ignore_space hist_ignore_all_dups \
  hist_save_no_dups hist_ignore_dups hist_find_no_dups

bindkey -v

# =========================================================
# Completion Styles
# =========================================================
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:z:*' fzf-preview 'eza -1 --group-directories-first --icons $realpath'
zstyle ':fzf-tab:complete:__zoxide_z:*' fzf-preview 'eza -1 --group-directories-first --icons $realpath'

# =========================================================
# ZLE / Keybindings
# =========================================================
# might be redundant, with the vim mode
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^x^e' edit-command-line
bindkey ' ' magic-space

# history search (fix strafing)
bindkey -M viins '^[[A' history-search-backward
bindkey -M viins '^[[B' history-search-forward

# =========================================================
# Named Directories
# =========================================================
hash -d dl=/mnt/c/Users/asiim/Downloads
hash -d sfx=/mnt/c/Users/asiim/Music/sfx
hash -d porn=/mnt/nuncipher/work

# =========================================================
# Plugin Management â€” Antidote
# =========================================================
zsh_plugins="${ZDOTDIR:-$HOME}/.zsh_plugins"

[[ -f ${zsh_plugins}.txt ]] || : > "${zsh_plugins}.txt"

fpath=(/usr/share/zsh-antidote/functions $fpath)
autoload -Uz antidote

if [[ ! ${zsh_plugins}.zsh -nt ${zsh_plugins}.txt ]]; then
  antidote bundle < "${zsh_plugins}.txt" >| "${zsh_plugins}.zsh"
fi
source "${zsh_plugins}.zsh"

# =========================================================
# Tool Initialization (interactive)
# =========================================================
eval "$(mise activate zsh)"
eval "$(mcfly init zsh)"
eval "$(zoxide init zsh)"
eval "$(thefuck --alias)"

# ssh-agent (silent)
eval "$(ssh-agent -s)" &>/dev/null

# Set up fzf key bindings and fuzzy completion
source <(fzf --zsh)

# Bun shell helpers
[[ -s $BUN_INSTALL/_bun ]] && source $BUN_INSTALL/_bun

# Broot
source ~/.config/broot/launcher/bash/br

# =========================================================
# Prompt
# =========================================================
[[ -f ~/.p10k.zsh ]] && source ~/.p10k.zsh

# =========================================================
# Functions
# =========================================================

# eza-backed ls
ls() { command eza -a --group-directories-first --icons "$@"; }

copyfile() {
  [[ $# -eq 1 && -f $1 ]] || return 1
  local cmd
  for cmd in clip.exe wl-copy xclip xsel pbcopy; do
    command -v $cmd &>/dev/null && { cat "$1" | $cmd; return }
  done
  echo "No clipboard utility found." >&2
}

copybuffer() {
  [[ -n $BUFFER ]] || return 1
  print -rn -- "$BUFFER" | clip.exe
  zle -M "Copied to Windows clipboard."
}
zle -N copybuffer
bindkey -M {emacs,viins,vicmd} '^O' copybuffer

fastfetch-widget() {
  zle reset-prompt
  fastfetch --config examples/13
  print
  zle reset-prompt
}
zle -N fastfetch-widget
bindkey '^[f' fastfetch-widget

# =========================================================
# personal scripts
# =========================================================
source ~/.local/bin/yt-stuff.zsh
source ~/.local/bin/git_switch.sh
 
# =========================================================
# MP4 Compressor
# =========================================================
compress_mp4() {
  local in=$1 crf=${2:-18}
  [[ -f $in ]] || return 1
  ffmpeg -i "$in" -c:v libx264 -preset slow -crf "$crf" \
    -pix_fmt yuv420p -movflags +faststart -c:a copy \
    "$(dirname "$in")/[compressed] $(basename "$in")"
}

# =========================================================
# Aliases
# =========================================================
alias c=clear reload='exec $SHELL' cd=z
alias v=$EDITOR vim=$EDITOR lg=lazygit
alias ll='eza -al --icons' lt='eza -a --tree --level=1 --icons'
alias nf=fastfetch ff=fastfetch pf=fastfetch
alias shutdown='systemctl poweroff'
alias wifi=nmtui cleanup='~/.config/ml4w/scripts/arch/cleanup.sh'
alias ts='~/.config/ml4w/scripts/arch/snapshot.sh'

alias ml4w='flatpak run com.ml4w.welcome'
alias ml4w-settings='flatpak run com.ml4w.settings'
alias ml4w-update='~/.config/ml4w/update.sh'

alias ascii='~/.config/ml4w/scripts/figlet.sh'
alias update-grub='sudo grub-mkconfig -o /boot/grub/grub.cfg'
alias res1='xrandr --output DisplayPort-0 --mode 2560x1440 --rate 120'
alias res2='xrandr --output DisplayPort-0 --mode 1920x1080 --rate 120'
alias celar=clear py=python

alias sshback='ssh-add ~/.ssh/id_ed25519_backend_cadera'
alias sshfront='ssh-add ~/.ssh/id_ed25519_frontend_cadera'

alias qt='gping ping.archlinux.org'
alias st=speedtest
alias ght='gh auth switch'

alias recite='eval $(poetry env activate)'
alias cm=chezmoi

